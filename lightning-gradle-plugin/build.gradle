apply plugin: 'maven'
apply plugin: 'java-gradle-plugin'
apply plugin: 'signing'

gradlePlugin {
    plugins {
        lightning {
            id = 'uk.co.deliverymind.lightning'
            implementationClass = 'uk.co.deliverymind.lightning.gradle.plugin.LightningPlugin'
        }
    }
}

test {
    useTestNG()

    doFirst {
        println "\nIf you execute this task for first time, it may take a good while to run. This is because our tests cover compatibility with different Gradle versions, which may need to be downloaded first. However, this will be handled automatically for you and will happen only once.\n"
    }
}

dependencies {
    compile "uk.co.deliverymind:lightning-core:${lightningCoreVersion}"
    testCompile 'org.testng:testng:6.10'
}

repositories {
    mavenCentral()
    mavenLocal()
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                group 'uk.co.deliverymind'
                name 'lightning-gradle-plugin'
                version lightningGradlePluginVersion
                packaging 'jar'
                description 'Gradle plugin encapsulating Lightning functionality'
                url 'http://deliverymind.github.io/lightning'

                scm {
                    connection 'scm:git:git@github.com:deliverymind/lightning.git'
                    developerConnection 'scm:git:git@github.com:deliverymind/lightning.git'
                    url 'git@github.com:deliverymind/lightning.git'
                }

                licenses {
                    license {
                        name 'MIT License'
                        url 'http://www.opensource.org/licenses/mit-license.php'
                    }
                }

                developers {
                    developer {
                        name 'Irek Pastusiak'
                        email 'irek@deliverymind.co.uk'
                        organizationUrl 'https://github.com/deliverymind'
                    }
                }
            }
        }
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}